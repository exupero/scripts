#!/usr/bin/env bb

; Sorts files in a .diff by the length of their changes

(ns diff-sort
  (:require [clojure.string :as str]
            diff))

(def sorters
  {:additions (fn [lines]
                (->> lines
                     str/split-lines
                     (filter #(str/starts-with? % "+"))
                     count
                     -))
   :alphabetical identity
   :deletions (fn [lines]
                (->> lines
                     str/split-lines
                     (filter #(str/starts-with? % "-"))
                     count
                     -))
   :similarity (fn [lines]
                 (let [additions (->> lines
                                      str/split-lines
                                      (filter #(str/starts-with? % "+"))
                                      count)
                       deletions (->> lines
                                      str/split-lines
                                      (filter #(str/starts-with? % "-"))
                                      count)]
                   (Math/abs (- additions deletions))))
   :size (comp count str/split-lines)
   :test (fn [lines]
           (let [[line] (str/split-lines lines)]
             (if (re-find #"\btests?\b" line) 1 0)))})

(let [[metric] *command-line-args*
      metric (or (some-> metric keyword) :size)]
  (->> (slurp *in*)
       diff/files
       (sort-by (sorters metric))
       (str/join "\n")
       println))
