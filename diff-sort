#!/usr/bin/env bb

; Sorts files in a .diff by a variety of metrics: number of additions,
; deletions, alphabetically, similarity, size, and whether it's a test.

(ns diff-sort
  (:require [clojure.string :as str]
            diff
            util))

(def sorters
  {:additions (fn [lines]
                (->> lines
                     str/split-lines
                     (filter #(str/starts-with? % "+"))
                     count
                     -))
   :alphabetical identity
   :deletions (fn [lines]
                (->> lines
                     str/split-lines
                     (filter #(str/starts-with? % "-"))
                     count
                     -))
   :similarity (fn [lines]
                 (let [additions (->> lines
                                      str/split-lines
                                      (filter #(str/starts-with? % "+"))
                                      count)
                       deletions (->> lines
                                      str/split-lines
                                      (filter #(str/starts-with? % "-"))
                                      count)]
                   (Math/abs (- additions deletions))))
   :size (comp count str/split-lines)
   :test (fn [lines]
           (let [[line] (str/split-lines lines)]
             (if (re-find #"\btests?\b" line) 1 0)))})

(let [[metric] *command-line-args*]
  (when-not metric
    (let [metrics (->> (keys sorters)
                       (map name)
                       (str/join ", "))]
      (util/abort (str "Must specify a sorting metric: " metrics))))
  (->> (slurp *in*)
       diff/files
       (sort-by (sorters (keyword metric)))
       (str/join "\n")
       println))
