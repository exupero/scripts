#!/usr/bin/env bb

; Sorts files in a .diff by the length of their changes

(ns diff-sort
  (:require [clojure.string :as str]
            diff))

(def sorters
  {:size (comp count str/split-lines)
   :deletions (fn [lines]
                (->> lines
                     str/split-lines
                     (filter #(str/starts-with? % "-"))
                     count
                     -))
   :similarity (fn [lines]
                 (let [additions (->> lines
                                      str/split-lines
                                      (filter #(str/starts-with? % "+"))
                                      count)
                       deletions (->> lines
                                      str/split-lines
                                      (filter #(str/starts-with? % "-"))
                                      count)]
                   (Math/abs (- additions deletions))))})

(let [[metric] *command-line-args*
      metric (or (some-> metric keyword) :size)]
  (->> (slurp *in*)
       diff/files
       (sort-by (sorters metric))
       (str/join "\n")
       println))
