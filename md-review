#!/usr/bin/env bb

(ns md-review
  (:require clojure.pprint
            [clojure.string :as str]
            [babashka.deps :as deps]
            [babashka.process :as p]
            [cheshire.core :as json]
            github
            http
            xforms))
(deps/add-deps '{:deps {net.cgrand/xforms {:mvn/version "0.19.6"}}})
(require '[net.cgrand.xforms :as xf])

(def alias->username
  {})

(defn replace-mentions [s]
  (str/replace s #"@([A-Za-z0-9_]+)"
               (fn [[_ nm]]
                 (str "@" (alias->username nm nm)))))

(defn parse-code-comment [lines]
  (let [[[header & code] [_ & cmt]]
        , (split-with (complement #{"```"}) lines)
        [_ path start-line] (re-find #"^```diff ([^ ]+) -\d+ \+(\d+)" header)
        start-line (parse-long start-line)
        right-side-lines (filter (partial re-find #"^$|^[^-]") code)
        end-line (+ start-line (dec (count right-side-lines)))]
    (cond-> {:body (->> cmt (str/join "\n") replace-mentions str/trim)
             :path path
             :side :RIGHT}
      (= start-line end-line)    (assoc :line start-line)
      (not= start-line end-line) (assoc :line end-line :start_line start-line))))

(defn parse-section-lines [lines]
  (if (re-find #"^```diff " (first lines))
    [:code (parse-code-comment lines)]
    [:main (-> (str/join "\n" lines) replace-mentions str/trim)]))

(defn parse-review [content]
  (into {}
        (comp
          (xforms/split-by #(re-find #"^---" %))
          (map (fn [section]
                 (->> section
                      (drop-while #{"---" ""})
                      parse-section-lines)))
          (xf/by-key (xf/into [])))
        (str/split-lines content)))

(defn fmt [{:keys [main code]}]
  {:body (str/join "\n\n" main)
   :comments code})

(let [review (parse-review (slurp *in*))
      {{owner :login} :headRepositoryOwner {repo :name} :headRepository :keys [number]}
      , (-> @(p/process {:out :string} "gh pr view --json headRepositoryOwner,headRepository,number")
            :out
            (json/parse-string true))]
  (clojure.pprint/pprint
    (-> (github/new-review owner repo number (fmt review))
        http/request!)))
