#!/usr/bin/env bb

(ns diff-ignore
  (:require [clojure.string :as str]
            diff
            xforms))

(defn ignorable? [regex lines]
  (let [original (diff/original lines)
        revised (diff/revised lines)]
    (and (= (count original) (count revised))
         (every? (fn [[original revised]]
                   (or (= original (str/replace revised regex ""))
                       (= revised (str/replace original regex ""))))
                 (map vector original revised)))))

(let [[pattern] *command-line-args*
      regex (re-pattern pattern)]
  (->> (slurp *in*)
       diff/files
       (transduce
         (comp
           (map diff/parse-file)
           (map (fn [{:keys [chunks] :as file}]
                  (update file :chunks (partial remove (fn [{:keys [lines]}]
                                                         (ignorable? regex lines))))))
           (remove (comp empty? :chunks))
           (map diff/unparse-file)
           (interpose "\n"))
         str)
       println))
