#!/usr/bin/env bb

(ns diff-ignore
  (:require [clojure.string :as str]
            diff
            xforms))

(defn substitute [s replacements]
  (reduce (fn [s [pattern replacement]]
            (str/replace s pattern replacement))
          s replacements))

(defn ignorable? [replacements lines]
  (let [original (diff/original lines)
        revised (diff/revised lines)]
    (and (= (count original) (count revised))
         (every? (fn [[original revised]]
                   (= original (substitute revised replacements)))
                 (map vector original revised)))))

(let [replacements (->> *command-line-args*
                        (partition 2)
                        (map (fn [[pattern replacement]]
                               [(re-pattern pattern) replacement])))]
  (->> (slurp *in*)
       diff/files
       (transduce
         (comp
           (map diff/parse-file)
           (map (fn [{:keys [chunks] :as file}]
                  (update file :chunks (partial remove (fn [{:keys [lines]}]
                                                         (ignorable? replacements lines))))))
           (remove (comp empty? :chunks))
           (map diff/unparse-file)
           (interpose "\n"))
         str)
       println))
